!   rbrconf.ecs

    script RBRConfigurator

    use graphics

    window Window
    window Dialog
    layout MainPanel
    layout LeftPanel
    layout RightPanel
    layout DeviceHPanel
    layout OuterLayout
    layout Layout
    layout VLayout
    layout HLayout
    groupbox Group
    label Label
    label RelayStateLabel
    label StatusLabel
    label UptimeLabel
    pushbutton ResetConfigButton
    pushbutton RBRNowDirectoryButton
    pushbutton ScanDevicesButton
    pushbutton ClearSystemButton
    pushbutton RemoveSlaveButton
    pushbutton UpdateFilesButton
    pushbutton DeleteFileButton
    pushbutton ExitButton
    pushbutton ScanSystemsButton
    pushbutton RemoveSystemButton
    pushbutton MasterDeviceButton
    pushbutton RelayOffButton
    pushbutton RelayOnButton
    pushbutton UpdateWidgetDataButton
    pushbutton OKButton
    pushbutton CancelButton
    lineinput LineInput
    lineinput DeviceNameInput
    lineinput LEDPinInput
    lineinput RelayPinInput
    lineinput DHT22PinInput
    lineinput PathInput
    listbox ListBox
    listbox SlaveList
    combobox SystemsCombo
    checkbox LEDInvertCheckbox
    checkbox RelayInvertCheckbox
    messagebox MessageBox

!    debug step

    log `Starting`
    init graphics
    create MainPanel type QHBoxLayout

    ! Do the left-hand panel

    create LeftPanel type QVBoxLayout
    add LeftPanel to MainPanel

    add stretch to LeftPanel

    create ResetConfigButton text `Reset everything`
    on click ResetConfigButton go to ResetConfigFileClick
    add ResetConfigButton to LeftPanel

    create RBRNowDirectoryButton text `Get the RBR-Now directory`
    on click RBRNowDirectoryButton go to RBRNowDirectoryClick
    add RBRNowDirectoryButton to LeftPanel

    create ClearSystemButton text `Clear the selected system`
    on click ClearSystemButton go to ClearSystemClick
    add ClearSystemButton to LeftPanel

    add stretch to LeftPanel

    create ScanDevicesButton text `Scan for devices`
    on click ScanDevicesButton go to ScanForDevicesClick
    add ScanDevicesButton to LeftPanel

    create UpdateFilesButton text `Update files`
    on click UpdateFilesButton go to UpdateFilesClick
    add UpdateFilesButton to LeftPanel

    create DeleteFileButton text `Delete file`
    add DeleteFileButton to LeftPanel
    on click DeleteFileButton go to DeleteFileClick

    create RemoveSlaveButton text `Remove slave`
    add RemoveSlaveButton to LeftPanel
    on click RemoveSlaveButton go to RemoveSlaveClick

    add stretch to LeftPanel

    create ExitButton text `Exit`
    add ExitButton to LeftPanel
    on click ExitButton go to ExitClick

    ! Now do the right-hand panel

    create RightPanel type QVBoxLayout
    add stretch RightPanel to MainPanel

    ! Create the system name group
    create Group title `Systems`
    set the height of Group to 50
    add Group to RightPanel
    create Layout type QHBoxLayout
    add Layout to Group
    create SystemsCombo
    add stretch SystemsCombo to Layout
    create ScanSystemsButton text `System Scan`
    disable ScanSystemsButton
    on click ScanSystemsButton go to ScanSystemsClick
    add ScanSystemsButton to Layout
    create RemoveSystemButton text `Remove`
    disable RemoveSystemButton
    on click RemoveSystemButton go to RemoveSystemClick
    add RemoveSystemButton to Layout
    
    ! Create the Master Device group
    create Group title `Master device`
    set the height of Group to 50
    add Group to RightPanel
    create Layout type QHBoxLayout
    add Layout to Group
    create MasterDeviceButton text ``
    add MasterDeviceButton to Layout
    on click MasterDeviceButton go to MasterDeviceClick
    
    ! Create the Slave Devices group
    create Group title `Slave devices`
    set the height of Group to 150
    add Group to RightPanel
    create Layout type QHBoxLayout
    add Layout to Group
    create SlaveList
    add SlaveList to Layout
    on select SlaveList go to SelectSlaveClick

    ! Create the Selected Device group
    create Group title `Selected device`
    set the height of Group to 150
    add Group to RightPanel

    create DeviceHPanel type QHBoxLayout
    add DeviceHPanel to Group

    create OuterLayout type QVBoxLayout
    add OuterLayout to DeviceHPanel

    create Layout type QHBoxLayout
    add Layout to OuterLayout
    create Label text `Name:`
    add Label to Layout
    create DeviceNameInput size 40
    add DeviceNameInput to Layout
    create Label text `Uptime:`
    add Label to Layout
    create UptimeLabel text `0`
    add UptimeLabel to Layout
    add stretch to Layout
    
    create Layout type QHBoxLayout
    add Layout to OuterLayout
    create Label text `LED Pin:`
    add Label to Layout
    create LEDPinInput size 5
    add LEDPinInput to Layout
    create LEDInvertCheckbox text `Inverted`
    add LEDInvertCheckbox to Layout
    add stretch to Layout
    
    create Layout type QHBoxLayout
    add Layout to OuterLayout
    create Label text `Relay Pin:`
    add Label to Layout
    create RelayPinInput size 5
    add RelayPinInput to Layout
    create RelayInvertCheckbox text `Inverted`
    add RelayInvertCheckbox to Layout
    create RelayOffButton size 5 text `-`
    on click RelayOffButton go to RelayOffClick
    add RelayOffButton to Layout
    create RelayOnButton size 5 text `+`
    on click RelayOnButton go to RelayOnClick
    add RelayOnButton to Layout
    create RelayStateLabel size 5 text `???`
    add RelayStateLabel to Layout
    add stretch to Layout
    
    create Layout type QHBoxLayout
    add Layout to OuterLayout
    create Label text `DHT22 Pin:`
    add Label to Layout
    create DHT22PinInput size 5
    add DHT22PinInput to Layout
    add stretch to Layout
    
    create Layout type QHBoxLayout
    add Layout to OuterLayout
    create Label text `Path:`
    add Label to Layout
    create PathInput size 60
    set the width of PathInput to 250
    add PathInput to Layout
    add stretch to Layout

    ! The 'Update' button
    create UpdateWidgetDataButton text `Update`
    on click UpdateWidgetDataButton go to UpdateWidgetDataClick
    add UpdateWidgetDataButton to DeviceHPanel

    add stretch to RightPanel

    create StatusLabel align right
    gosub to OK 
    add StatusLabel to RightPanel

    create Window title `RBR-Now configurator` size 700 500
    set the layout of Window to MainPanel
    show Window

    start graphics

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    variable Config
    variable ConfigFile
    variable FirmwareDirectory
    variable Hosts
    variable Name
    variable Value
    variable ReturnValue
    variable Title
    variable Result
    variable List
    variable Item
    variable HostSSID
    variable HostPassword
    variable SystemHostSSID
    variable SSID
    variable MAC
    variable Password
    variable IPAddr
    variable MyIPAddr
    variable Prompt
    variable SystemConfig
    variable SystemCallResult
    variable SystemName
    variable SystemMAC
    variable SystemPassword
    variable SystemIPAddr
    variable StatusMessage
    variable Message
    variable Message2
    variable Response
    variable Systems
    variable DeviceSelected
    variable Devices
    variable Device
    variable MasterDevice
    variable MasterSSID
    variable MasterMAC
    variable MasterIPAddr
    variable MasterDeviceName
    variable SlaveDevices
    variable CurrentSSID
    variable Previous
    variable IsMaster
    variable FileName
    variable FileSize
    variable ErrorFlag
    variable Attempt
    variable Sequence
    variable Start
    variable Finish
    variable Total
    variable Segment
    variable Length
    variable Path
    variable Files
    variable Count
    variable Content
    variable Uptime
    variable APInfo
    variable Info
    variable Keys
    variable URL
    variable Pins
    variable Pin
    variable D
    variable F
    variable K
    variable N
    variable P
    variable S

!    debug step

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   The main program starts here
Start:

    set the encoding to `hex`

!   Load the main config file or create one if there is none
    put trim system `echo $HOME` cat `/.rbr.conf` into ConfigFile
    load Config from ConfigFile
    if Config is empty put json `{}` into Config else put json Config into Config
    if Config does not have property `hosts` set property `hosts` of Config to json `{}`
    if Config does not have property `systems` set property `systems` of Config to json `{}`
    if Config does not have property `current-system` set property `current-system` of Config to empty
    put property `hosts` of Config into Hosts

    put empty into SystemHostSSID
    put empty into SystemIPAddr
    clear DeviceSelected

    gosub to SetBusy

!   Get the host SSID
    put `Getting host SSID...` into StatusMessage
    gosub to Working
    put trim system `LANG=C nmcli -t -f active,ssid dev wifi | grep ^yes | cut -d: -f2-` into HostSSID
    if Hosts has property HostSSID put property HostSSID of Hosts into HostPassword
    else
    begin
        gosub to GetHostPassword
        if HostPassword is not empty
        begin
            set property HostSSID of Hosts to HostPassword
            set property `hosts` of Config to Hosts
        end
    end
    save prettify Config to ConfigFile
    put `Connected to ` cat HostSSID into StatusMessage
    gosub to Idle

    gosub to CheckFirmware

!   Get the list of systems from Config, or create an empty list. Then populate the combo box
    if Config has property `systems` put property `systems` of Config into Systems
    else
    begin
        put json `{}` into Systems
        set property `systems` of Config to Systems
    end
    gosub to RefreshSystemsCombo

!   Select the current system
    if the count of SystemsCombo is 0 put empty into SystemName
    else
    begin
        put property `current-system` of Config into SystemName
        if SystemName is not empty
        begin
            select SystemName in SystemsCombo
!            log prettify SystemConfig
            gosub to SelectSystem
        end
    end

    gosub to ManageButtonStates
    on select SystemsCombo go to SystemsComboSelect
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Reset the entire system
ResetConfigFileClick:
    create MessageBox on Window
        style question
        title `Master reset`
        message `This will remove all information. Continue?`
    show MessageBox giving Value
    if Value is `Yes`
    begin
        delete file ConfigFile
        disable RemoveSystemButton
        set the text of MasterDeviceButton to empty
        put empty into SystemMAC
        clear SlaveList
        go to Start
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Scan the local network for system controllers
ScanSystemsClick:
    gosub to SetBusy
    log `Scan for system controllers`
    disable ScanSystemsButton
    put 0 into Count
    gosub to ScanSystems
    if Count is 0
    begin
        put `No new systems were found` into StatusMessage
        gosub to Error
    end
    gosub to RefreshSystemsCombo
    gosub to PostConfigData
    gosub to ManageButtonStates
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Scan the local network for RBR system controllers
ScanSystems:
    log `Scan systems`
    put trim system `hostname -I` into MyIPAddr
    log MyIPAddr
    put 1 into N
    while N is less than 255
    begin
        put the position of the last `.` in MyIPAddr into P
        increment P
        put left P of MyIPAddr into Value
        put Value cat N into IPAddr
        put `Scanning ` cat IPAddr into StatusMessage
        gosub to Working
        get Result from url `http://` cat IPAddr cat `:17348/cgi-bin/mac.py` timeout 1 or go to SS2
        increment Count
        put trim Result into Result
        log IPAddr cat `: ` cat Result
        put the position of ` ` in Result into P
        put left P of Result into SystemMAC
        increment P
        put from P of Result into Result
        put the position of ` ` in Result into P
        put left P of Result into SystemPassword
        increment P
        put from P of Result into SystemName
        put json `{}` into SystemConfig
        set property `host-ssid` of SystemConfig to HostSSID
        set property `host-password` of SystemConfig to HostPassword
        set property `ipaddr` of SystemConfig to IPAddr
        set property `mac` of SystemConfig to SystemMAC
        set property `password` of SystemConfig to SystemPassword
        set property `devices` of SystemConfig to json `{}`
        log `Adding ` cat SystemName cat ` to Systems`
        gosub to GetConfigData
!        log prettify SystemConfig
        set property SystemName of Systems to SystemConfig
    SS2:
        wait 10 ticks
        increment N
    end
    set property `current-system` of Config to SystemName
    gosub to UpdateSystems
!    log `After scan: ` cat prettify Config
    gosub to PostConfigData
    go to OK

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Get the config data for a system
GetConfigData:
    gosub to SetBusy
    if SystemName is empty return
    log prettify SystemConfig
    log `Get the config data for ` cat SystemName
    get Value from url `https://rbrheating.com/ui/resources/php/rest.php/config/`
        cat SystemMAC cat `/` cat SystemPassword
    or
    begin
        put `Can't read the config file for ` cat SystemName cat `(` cat MAC cat `/` cat Password cat `)` into StatusMessage
        go to Error
    end
!    log Value
    ! If the server has data, copy it into the local config
    if Value is `""` put empty into Value
    if Value is empty put `{}` into Value
    if Value is not `{}`
    begin
        log `Add config data from the server`
        put json Value into Value
!        log prettify Value
        put the keys of Value into Keys
        put 0 into K
        while K is less than the count of Keys
        begin
            put element K of Keys into Name
            log `Adding ` cat Name cat ` ` cat property Name of Value
!            if Name is `ssid` set property `host-ssid` of SystemConfig to property Name of Value
!            else if Name is `password` set property `host-password` of SystemConfig to property Name of Value
!            else
            set property Name of SystemConfig to property Name of Value
            increment K
        end
    end
    gosub to UpdateSystems
    put property `devices` of SystemConfig into Devices
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Refresh the systems combobox
RefreshSystemsCombo:
    log `Refresh SystemsCombo`
    clear SystemsCombo
    put the keys of Systems into Keys
    put 0 into S
    while S is less than the count of Keys
    begin
        put element S of Keys into Name
        log `Add ` cat Name cat ` to SystemsCombo`
        add Name to SystemsCombo
        increment S
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Select a system from the combo box
SystemsComboSelect:
    gosub to ResumeSystemController
    put SystemsCombo into SystemName
    if SystemName is not empty gosub to SelectSystem
    gosub to ManageButtonStates
    if not ErrorFlag gosub to OK
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Select a system
SelectSystem:
    log `Select system ` cat SystemName
    set property `current-system` of Config to SystemName
    put property SystemName of Systems into SystemConfig
    put property `ipaddr` of SystemConfig into SystemIPAddr
    put property `mac` of SystemConfig into SystemMAC
    put property `password` of SystemConfig into SystemPassword
    gosub to PopulateSystemInfo
    gosub to PostConfigData
    gosub to PauseSystemController
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Remove a system from the combo box
RemoveSystemClick:
    gosub to SetBusy
    put SystemsCombo into Name
    log `Remove ` cat Name cat `?`
    create MessageBox on Window
        style question
        title `Remove system`
        message `Continue you want to remove ` cat Name cat `?`
    show MessageBox giving Value
    if Value is `Yes`
    begin
        remove the current item from SystemsCombo
        delete property SystemName of Systems
        put empty into SystemName
        put empty into SystemConfig
        set the text of MasterDeviceButton to empty
        clear SlaveList
        set property `current-system` of Config to SystemsCombo
        set property `systems` of Config to Systems
        put empty into SystemHostSSID
        gosub to PostConfigData
    end
    if the count of SystemsCombo is 0 disable RemoveSystemButton
    else
    begin
        select index 0 of SystemsCombo
    end
    gosub to ManageButtonStates
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Clear all the data for the selected system
ClearSystemClick:
    gosub to SetBusy
    create MessageBox on Window
        style question
        title `System clear`
        message `This will remove all devices from ` cat SystemsCombo 
            cat `. Continue?`
    show MessageBox giving Value
    if Value is `Yes`
    begin
        if CurrentSSID is not HostSSID gosub to ConnectToHost
        put property SystemName of Systems into SystemConfig
        put property `mac` of SystemConfig into SystemMAC
        put property `password` of SystemConfig into SystemPassword
        put json `{}` into Devices
        gosub to UpdateSystemConfig
        set the text of MasterDeviceButton to empty
        put empty into MasterSSID
        clear SlaveList
        gosub to PostConfigData
    end
    gosub to OK
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Get the RBR-Now directory and save it.
RBRNowDirectoryClick:
    gosub to GetFirmware
    gosub to CheckFirmware3
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Check if we have a valid firmware directory.
CheckFirmware:
    if Config has property `firmware` put property `firmware` of Config into FirmwareDirectory
    else put empty into FirmwareDirectory
CheckFirmware2:
    if file FirmwareDirectory cat `/files.txt` exists
    begin
        set property `firmware` of Config to FirmwareDirectory
        save prettify Config to ConfigFile
        go to OK
    end
    put `Firmware directory is missing or unspecified` into StatusMessage
    gosub to Error
    gosub to GetFirmware
CheckFirmware3:
    if ReturnValue is empty return
    put trim system `echo $HOME` cat `/` cat ReturnValue into FirmwareDirectory
    go to CheckFirmware2

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Get the RBR-Now firmware
GetFirmware:
    put `RBR-Now firmware` into Title
    put `Get the directory containing the RBR-Now firmware`
        cat newline cat `(relative to your home directory)` into Prompt
    put 80 into Length
    go to GetRequestedText

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Scan the local network for new devices
ScanForDevicesClick:
    gosub to SetBusy
    put `Scanning for new devices...` into StatusMessage
    gosub to Working
    put system `nmcli device wifi list` into SystemCallResult
    log SystemCallResult
    split SystemCallResult
    put json `[]` into List
    put 1 into N
    while N is less than the elements of SystemCallResult
    begin
        index SystemCallResult to N
        put SystemCallResult into SSID
        if left 1 of SSID is `*` begin end
        else
        begin
            ! We only want RBR-Now-xxxxxxxxxxxx devices
            put trim SSID into SSID
            put the position of ` ` in SSID into P
            put from P of SSID into SSID
            put trim SSID into SSID
            put the position of ` ` in SSID into P
            put left P of SSID into SSID
            if left 7 of SSID is `RBR-Now`
            begin
                log `Found ` cat SSID
                ! Check if this one already exists
                put the keys of Devices into Keys
                put 0 into D
                while D is less than the count of Keys
                begin
                    put element D of Keys into Name
                    put property Name of Devices into Device
                    if SSID is property `ssid` of Device
                    begin
                        log SSID cat ` exists already`
                        go to SFDC2
                    end
                    increment D
                end
                append SSID to List
            end
        end
    SFDC2:
        increment N
    end
    log `SSIDs: ` cat List
    if the count of List is 0
    begin
        put `No new devices were found` into StatusMessage
        log StatusMessage
        gosub to Idle
        stop
    end
    put `Select Device` into Title
    put `Select a new device` into Prompt
    gosub to SelectSSIDFromList
    enable ClearSystemButton
    if SSID is empty
    begin
        gosub to OK
        stop
    end
    else 
    begin
        gosub to SetupDevice
        if ErrorFlag go to SFDC3
        gosub to ConnectToHost
        gosub to PostConfigData
    end
SFDC3:
    gosub to ManageButtonStates
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Select an SSID from the list
SelectSSIDFromList:
    create Layout type QVBoxLayout
    create Label text Prompt align center
    add Label to Layout
    create ListBox
    put 0 into N
    while N is less than the count of List
    begin
        add element N of List to ListBox
        increment N
    end
    on select ListBox
    begin
        put the current item in ListBox into SSID
        log `Selected ` cat SSID
        put SSID cat ` Selected ` into StatusMessage
        gosub to Working
        close Dialog
        return
    end
    add ListBox to Layout
    add stretch to Layout
    create HLayout type QHBoxLayout
    create CancelButton text `Cancel`
    on click CancelButton
    begin
        put empty into Item
        close Dialog
        return
    end
    add CancelButton to HLayout
    add HLayout to Layout
    create Dialog title Title size 250 200
    set the layout of Dialog to Layout
    center Dialog on Window
    show Dialog
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Set up the device identified by SSID
SetupDevice:
    if SSID is HostSSID
    begin
        put `Oops - this is the host!` into StatusMessage
        go to Error
    end

    ! Connect to the device's AP and get basic data
    gosub to ConnectToDevice
    if ErrorFlag return
    put system `hostname -I` into IPAddr
    put the position of the last `.` in IPAddr into D
    increment D
    put left D of IPAddr into IPAddr
    put IPAddr cat `1` into IPAddr
    put `http://` cat IPAddr into URL
    put `Request AP details for ` cat SSID cat ` at ` cat URL into StatusMessage
    gosub to Working
    get APInfo from url URL
    or begin
        put `Failed to interrogate ` cat SSID into StatusMessage
        go to Error
    end
    put trim APInfo into APInfo
    log `Device info: ` cat APInfo
    put APInfo into Info
    split Info on ` `
    if the elements of Info is less than 4
    begin
        put `Bad response from device: ` cat APInfo into StatusMessage
        go to Error
    end
    index Info to 2
    put Info into Name
    index Info to 0
    put Info into MAC

    ! Check if this is the first device to be configured. If so, make it the master.
    if MasterSSID is empty
    begin
        set IsMaster
        log `Deal with master ` cat Name
        put SSID into MasterSSID
        put Name into MasterDeviceName
        gosub to CreateDevice
        if ErrorFlag return
        put Device into MasterDevice
        put MAC into MasterMAC
        put IPAddr into MasterIPAddr
        if ErrorFlag return

        ! Reboot the device
        gosub to RequestReset
        if ErrorFlag return
        put `Give the device a few seconds to reboot` into StatusMessage
        gosub to Working
        wait 10
        gosub to ConnectToDevice
        if ErrorFlag return
        put MasterSSID cat ` ` cat MasterName into StatusMessage
        gosub to Working
        disable MasterDeviceButton

        ! Get the master IP address
        put `http://` cat MasterIPAddr cat `/?mac=` cat MasterMAC cat `&msg=ipaddr` into URL
        put `Interrogate the master device` into StatusMessage
        gosub to Working
        get Response from url URL
        or begin
            put `Failed to get Master ip address of ` cat MasterSSID into StatusMessage
            go to Error
        end
        if left 2 of Response is `OK`
        begin
            put from 3 of Response into MasterIPAddr
            put `Master ip address is ` cat MasterIPAddr into StatusMessage
            gosub to Working
            gosub to SetMasterDeviceButtonText
            set property `ipaddr` of MasterDevice to MasterIPAddr
            set property MasterDeviceName of Devices to MasterDevice
            gosub to UpdateSystemConfig
        end
        else
        begin
            put `Failed to get Master ip address` into StatusMessage
            go to Error
        end
        enable MasterDeviceButton
        put MasterDevice into Device
        gosub to PopulateDeviceInfo
    end
    else
    begin
        clear IsMaster
        log `Deal with slave ` cat Name
        put the position of ` ` in Name into N
        increment N
        put SSID cat ` ` cat from N of Name into Item
        if SlaveDevices does not include Item
        begin
            append Item to SlaveDevices
            set SlaveList to SlaveDevices
            gosub to CreateDevice
        end
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Create a device entry in the config file
CreateDevice:
    log `Create Device`
    put json `{}` into Device
    if IsMaster
    begin
        log `Create master device ` cat MastertDeviceName
        set property `hostssid` of Device to HostSSID
        set property `hostpass` of Device to HostPassword
        set property `ipaddr` of Device to empty
    end
    else log `Create slave device ` cat Name
    set property `master` of Device to IsMaster
    set property `name` of Device to Name
    set property `ssid` of Device to SSID
    set property `channel` of Device to 1
    put json `{}` into Pins
    put json `{}` into Pin
    set property `pin` of Pin to empty
    set property `invert` of Pin to false
    set property `led` of Pins to Pin
    put json `{}` into Pin
    set property `pin` of Pin to empty
    set property `invert` of Pin to false
    set property `relay` of Pins to Pin
    put json `{}` into Pin
    set property `pin` of Pin to empty
    set property `dht22` of Pins to Pin
    set property `pins` of Device to Pins
    set property Name of Devices to Device
    go to UpdateSystemConfig

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Populate the system information fields
PopulateSystemInfo:
    log `Populate fields for ` cat SystemName
    put property `host-ssid` of SystemConfig into SystemHostSSID
    put property `host-password` of SystemConfig into Password
    if SystemHostSSID is not HostSSID
    begin
        put `Different host SSID: ` cat SystemHostSSID into StatusMessage
        gosub to Working
        create MessageBox on Window
            style question
            title `Move system`
            message `Shall I move system ` cat SystemName cat ` to the current host (` cat HostSSID cat `)?` cat newline
                cat `(If 'No' I will attempt to connect to ` cat SystemHostSSID cat `)`
        show MessageBox giving Value
        if Value is `Yes`
        begin
            put HostSSID into SystemHostSSID
            set property `host-ssid` of SystemConfig to HostSSID
            set property `host-password` of SystemConfig to HostPassword
            gosub to UpdateSystems
        end
        else
        begin
            put SystemHostSSID into HostSSID
            put Password into HostPassword
            system `nmcli connection delete ` cat HostSSID
            put system `nmcli dev wifi connect ` cat HostSSID cat ` password ` cat HostPassword into SystemCallResult
            split SystemCallResult on ` `
            if the elements of SystemCallResult is greater than 2
            begin
                index SystemCallResult to 2
                if SystemCallResult is `successfully`
                begin
                    set property HostSSID of Hosts to HostPassword
                    set property `hosts` of Config to Hosts
                    gosub to PostConfigData
                    put HostSSID into CurrentSSID
                    put `Connected to ` cat HostSSID into StatusMessage
                    gosub to Working
                end
                else
                begin
                    put `Failed to connect to ` cat HostSSID into StatusMessage
                    go to Error
                end
            end
        end
    end
    put property `mac` of SystemConfig into SystemMAC
    put property `password` of SystemConfig into SystemPassword
    put property `devices` of SystemConfig into Devices
    put the keys of Devices into Keys
    put empty into MasterDevice
    set the text of MasterDeviceButton to empty
    put json `[]` into SlaveDevices
    clear SlaveList
    put 0 into D
    while D is less than the count of Keys
    begin
        put element D of Keys into Name
        put property Name of Devices into Device
        if property `master` of Device
        begin
            put Device into MasterDevice
            put property `name` of MasterDevice into MasterDeviceName
            put property `ssid` of MasterDevice into MasterSSID
            put property `ipaddr` of MasterDevice into MasterIPAddr
            gosub to SetMasterDeviceButtonText
        end
        else
        begin
            put property `ssid` of Device into SSID
            append SSID cat ` ` cat Name to SlaveDevices
            add SSID cat ` ` cat Name to SlaveList
        end
        increment D
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update text on MasterDeviceButton
SetMasterDeviceButtonText:
!    set the text of MasterDeviceButton to MasterSSID cat ` ` cat MasterIPAddr cat ` ` cat MasterDeviceName
    set the text of MasterDeviceButton to MasterSSID cat ` ` cat MasterDeviceName
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Handle a click on the MasterDeviceButton
MasterDeviceClick:
    if MasterDeviceButton is not empty
    begin
        log `Select the master device`
        set DeviceSelected
        set IsMaster
        put MasterDevice into Device
        gosub to PopulateDeviceInfo
    end
    gosub to ManageButtonStates
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Populate the device information fields
PopulateDeviceInfo:
    gosub to GetDeviceData
    if Device has property `name`
    begin
        log `Populate device ` cat property `name` of Device
        gosub to CheckDevice
        set the text of DeviceNameInput to property `name` of Device
        set the text of UptimeLabel to Uptime
        set the text of RelayStateLabel to `???`
        set the text of PathInput to property `path` of Device
        put property `pins` of Device into Pins
        put property `led` of Pins into Pin
        set the text of LEDPinInput to property `pin` of Pin
        set the state of LEDInvertCheckbox to property `invert` of Pin
        put property `relay` of Pins into Pin
        set the text of RelayPinInput to property `pin` of Pin
        set the state of RelayInvertCheckbox to property `invert` of Pin
        put property `dht22` of Pins into Pin
        set the text of DHT22PinInput to property `pin` of Pin
        enable UpdateFilesButton
        enable DeleteFileButton
    end
    go to OK

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Show information about a slave device
SelectSlaveClick:
    log `Select ` cat SlaveList
    set DeviceSelected
    clear IsMaster
    put SlaveList into Item
    split Item on ` `
    index Item to 1
    put property Item of Devices into Device
    set the elements of Item to 1
    gosub to PopulateDeviceInfo
    gosub to ManageButtonStates
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Remove a slave device from the list
RemoveSlaveClick:
    put SlaveList into Name
    put Name into Item
    split Item on ` `
    index Item to 1
    put Item into Name
    set the elements of Item to 1
    put the keys of Devices into Keys
    put 0 into D
    while D is less than the count of Keys
    begin
        put element D of Keys into Item
        put property Item of Devices into Device
        if property `master` of Device is false
        begin
            if property `name` of Device is Name
            begin
                log `Deleting ` cat Name
                delete property Item of Devices
                delete element SlaveList of SlaveDevices
                set SlaveList to SlaveDevices
                gosub to UpdateSystemConfig
                gosub to PostConfigData
                stop
            end
        end
        increment D
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Close the application and exit
ExitClick:
    log `Exit the configurator`
    gosub to ResumeSystemController
    close Window
    exit

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Get the host password
GetHostPassword:
    log `Get the host password`
    put `Type the host password:` into Prompt
    put `Host password` into Title
    put 40 into Length
    gosub to GetRequestedText
    put ReturnValue into HostPassword
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Get user input
GetRequestedText:
    create VLayout type QVBoxLayout
    create Label text Prompt align centre
    add Label to VLayout
    create HLayout type QHBoxLayout
    add HLayout to VLayout
    create LineInput size Length
    add stretch LineInput to HLayout
    create HLayout type QHBoxLayout
    add HLayout to VLayout
    create OKButton text `OK` size 6
    on click OKButton
    begin
        put LineInput into ReturnValue
        close Dialog
        return
    end
    add OKButton to HLayout
    create CancelButton text `Cancel` size 6
    on click CancelButton
    begin
        put empty into ReturnValue
        close Dialog
        return
    end
    add CancelButton to HLayout
    create Dialog title Title size 300 80
    set the layout of Dialog to VLayout
    center Dialog on Window
    show Dialog
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Scan the network for access points
GetAccessPoints:
    put `Scanning the local network...` into StatusMessage
    log StatusMessage
    gosub to Working
    put system `nmcli device wifi list` into Result
!    log Result
    gosub to OK
    split Result
    put json `[]` into List
    put 1 into N
    while N is less than the elements of Result
    begin
        index Result to N
        put Result into SSID
        if left 1 of SSID is `*` begin end
        else
        begin
            put trim SSID into SSID
            put the position of ` ` in SSID into P
            put from P of SSID into SSID
            put trim SSID into SSID
            put the position of ` ` in SSID into P
            put left P of SSID into SSID
            if SSID is empty go to SN2
            if left 1 of SSID is `-` go to SN2
            append SSID to List
        end
    SN2:
        increment N
    end
!    log `SSIDs: ` cat List
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Connect to the host router
ConnectToHost:
    put `Connect to host ` cat HostSSID cat ` with password ` cat HostPassword into StatusMessage
    gosub to Working
    system `nmcli connection delete ` cat HostSSID
    put system `nmcli dev wifi connect ` cat HostSSID cat ` password ` cat HostPassword into SystemCallResult
    split SystemCallResult on ` `
    if the elements of SystemCallResult is greater than 2
    begin
        index SystemCallResult to 2
        if SystemCallResult is `successfully`
        begin
            put HostSSID into CurrentSSID
            put `Connected to ` cat HostSSID into StatusMessage
            go to Idle
        end
    end
    put `Failed to connect to ` cat HostSSID into StatusMessage
    gosub to GetAccessPoints
    put `Select HostSSID` into Title
    put `Select the home router for this system` into Prompt
    gosub to SelectSSIDFromList
    if HostSSID is empty go to Start
    set property `host-ssid` of Config to HostSSID
    if Config does not have property `host-password` gosub to GetHostPassword
    set property `host-password` of Config to HostPassword
    go to ConnectToHost

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Here when a connection failure occurs
FailedToConnect:
    log `Result: ` cat SystemCallResult
    set the elements of Value to 1
    put `Failed to connect to ` cat SSID into StatusMessage
    go to Error

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Connect to a device
ConnectToDevice:
    put `Connect to device ` cat SSID cat ` with password 00000000` into StatusMessage
    gosub to Working
    system `nmcli connection delete ` cat SSID
    put system `nmcli dev wifi connect ` cat SSID cat ` password 00000000` into SystemCallResult
    put SystemCallResult into Value
    split Value on ` `
    if the elements of Value is not greater than 2 go to FailedToConnect
    index Value to 2
    if Value is not `successfully` go to FailedToConnect
    put SSID into CurrentSSID
    put `Connected to ` cat SSID into StatusMessage
    gosub to Working
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update the widget data for the selected device
UpdateWidgetDataClick:
    gosub to SetBusy
    put property `name` of Device into Previous
    set property `name` of Device to DeviceNameInput
    set property `path` of Device to PathInput
    put json `{}` into Pins
    put json `{}` into Pin
    set property `pin` of Pin to LEDPinInput
    set property `invert` of Pin to LEDInvertCheckbox
    set property `led` of Pins to Pin
    put json `{}` into Pin
    set property `pin` of Pin to RelayPinInput
    set property `invert` of Pin to RelayInvertCheckbox
    set property `relay` of Pins to Pin
    put json `{}` into Pin
    set property `pin` of Pin to DHT22PinInput
    set property `dht22` of Pins to Pin
    set property `pins` of Device to Pins
    put `config.json` into FileName
    put stringify Device into Content
    gosub to SendOneFile
    if not ErrorFlag
    begin
        set property DeviceNameInput of Devices to Device
        if IsMaster
        begin
            put DeviceNameInput into MasterDeviceName
            gosub to SetMasterDeviceButtonText
        end
        if DeviceNameInput is not Previous delete property Previous of Devices
        gosub to UpdateSystemConfig
        gosub to PostConfigData
        gosub to RequestReset
    end
    gosub to Idle
    gosub to ManageButtonStates
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update all the files on a device
UpdateFilesClick:
    if not DeviceSelected stop
    put property `ssid` of Device into SSID
    put from 8 of SSID into MAC

    put FirmwareDirectory cat `/files.txt` into Path
    load Files from Path
    split Files
    put 0 into F
    while F is less than the elements of Files
    begin
        index Files to F
        put trim Files into FileName
        if FileName is not empty gosub to UpdateOneFile
        if ErrorFlag
        begin
            put `File updates did not succeed` into StatusMessage
            gosub to Error
            gosub to ManageButtonStates
            stop
        end
        increment F
    end
    gosub to RequestReset
    gosub to ManageButtonStates
    gosub to Idle
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Update a single file
UpdateOneFile:
    put FirmwareDirectory cat `/` cat FileName into Path
    if file Path exists begin end
    else
    begin
        log `File ` cat Path cat ` does not exist`
        return
    end
    load Content from Path

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Send a single file with multiple attempts
SendOneFile:
    put 0 into Attempt
    clear ErrorFlag
SOF2:
    increment Attempt
    gosub to SendFileToDevice
    if ErrorFlag
    begin
        if Attempt is less than 10 go to SOF2
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Send a file to a device
SendFileToDevice:
    gosub to SetBusy
    put the length of Content into FileSize
    put `Update device file ` cat FileName cat ` (size=` cat FileSize cat ` chars)` into StatusMessage
    gosub to Working
    wait 1
    put 0 into Sequence
    put 0 into Start
    put 0 into Total
    while Start is less than FileSize
    begin
        if Finish is greater than FileSize put FileSize into Finish
        add 100 to Start giving Finish
        put from Start to Finish of Content into Segment
        put the length of Segment into Length
        add Length to Total
        put encode Segment into Segment
        put `Send ` cat FileName cat `, part ` cat Sequence into StatusMessage
        if Attempt is not 1 put StatusMessage cat ` (attempt ` cat Attempt cat `)` into StatusMessage
        gosub to Working
        put `part:`  cat Sequence cat `,text:` cat Segment into Message
        gosub to SendMessageToDevice
        if Response is empty
        begin
            put `Failed to send part ` cat Sequence into StatusMessage
            go to SendFileFail
        end
!        log `Part ` cat Sequence cat `: ` cat Response
        split Response on ` `
        index Response to 0
        if Response is not Length
        begin
            put `Mismatched length in part ` cat Sequence into StatusMessage
            go to SendFileFail
        end
        put Finish into Start
        increment Sequence
    end
    put `save:` cat FileName into Message
    gosub to SendMessageToDevice
    split Response on ` `
    index Response to 0
    if Response is not Total
    begin
        put `Mismatched length: ` cat Total cat ` - ` cat Response into StatusMessage
        go to SendFileFail
    end
    put `File ` cat FileName cat ` saved` into StatusMessage
    gosub to Working
    return

SendFileFail:
    put FileName cat `: ` cat StatusMessage into StatusMessage
    gosub to Error
    wait 5
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Delete a file on the selected device
DeleteFileClick:
    put `Name of file to delete:` into Prompt
    put `Delete file` into Title
    put 40 into Length
    gosub to GetRequestedText
    if ReturnValue is not empty
    begin
        put ReturnValue into FileName
        put `Delete  ` cat FileName into StatusMessage
        gosub to Working
        wait 1
        put `delete:` cat FileName into Message
        gosub to SendRBRMessage
        if left 2 of Response is not `OK`
        begin
            put `Failed to delete ` cat FileName into StatusMessage
            gosub to Error
            stop
        end
        put `File ` cat FileName cat ` has been deleted` into StatusMessage
        gosub to Idle
    end
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Send a reset message to a device
RequestReset:
    gosub to GetDeviceData
    put `Reset ` cat Name into StatusMessage
    gosub to Working
    wait 1
    put `reset` into Message
    gosub to SendRBRMessage
    if left 2 of Response is not `OK`
    begin
        put `Failed to reset ` cat Name into StatusMessage
        go to Error
    end
    put `Device ` cat Name cat ` has been reset` into StatusMessage
    go to Working

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Check a device
CheckDevice:
    put `uptime` into Message
    gosub to SendRBRMessage
    if Response is empty
    begin
        put `No response from ` cat Name into StatusMessage
        gosub to Error
        put `0` into Uptime
    end
    else put from 3 of Response into Uptime

CheckDevice2:
    put the position of ` ` in Uptime into P
    if P is greater than 0
    begin
        put left P of Uptime into Uptime
        put Uptime cat ` sec` into Uptime
    end
    else put `Unknown` into Uptime
!    log Name cat ` uptime: ` cat Uptime
    set the text of UptimeLabel to Uptime
    go to OK

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Turn a relay OFF
RelayOffClick:
    log `Turn the relay OFF`
    put `off` into Message
    gosub to SendRBRMessage
    log `ON: ` cat Response
    if Response is empty
    begin
        put `No response from ` cat Name into StatusMessage
        gosub to Error
        put `0` into Uptime
    end
    begin
        put from 3 of Response into Uptime
        put Uptime cat ` sec` into Uptime
        set the text of RelayStateLabel to `OFF`
    end
    gosub to CheckDevice2
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Turn a relay ON
RelayOnClick:
    log `Turn the relay ON`
    put `on` into Message
    gosub to SendRBRMessage
    log `ON: ` cat Response
    if Response is empty
    begin
        put `No response from ` cat Name into StatusMessage
        gosub to Error
        put `0` into Uptime
    end
    else
    begin
        put from 3 of Response into Uptime
        put Uptime cat ` sec` into Uptime
        set the text of RelayStateLabel to `ON`
    end
    gosub to CheckDevice2
    stop

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Send an RBR-Now message to a device
SendRBRMessage:
    gosub to SendMessageToDevice
!    log URL cat ` -> ` cat Response
    if left 2 of Response is `OK` return
    put `Bad response from ` cat Name into StatusMessage
    gosub to Error
    if Path is not empty
    begin
        log `Retry without path`
        set property `path` of Device to empty
        go to SendMessageToDevice
    end
    put empty into Response
    set ErrorFlag
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Send a message to a RBR-Now device
SendMessageToDevice:
    gosub to GetDeviceData
    put property `ssid` of Device into SSID
    put from 8 of SSID into MAC
    put left 30 of Message into Message2
    if the length of Message is greater than 30 put Message2 cat `...` into Message2
    put Message into Message2
    if Path is not empty
    begin
        put the position of `,` in Path into P
        if P is greater than 0
        begin
            put left P of Path into MAC
            add 1 to P
            put from P of Path into Path
            put `!` cat Path cat `,` cat Message into Message2
        end
        else
        begin
            put `!` cat MAC cat `,` cat Message into Message2
            put Path into MAC
        end
    end
    put empty into Response
    put `http://` cat MasterIPAddr cat `/?mac=` cat MAC cat `&msg=` cat Message2 into URL
!    log URL
    get Response from url URL
    or begin
        put `Could not message ` cat Name into StatusMessage
        gosub to Working
        ! Try direct communication
        if Path is not empty
        begin
            set property `path` of Device to empty
            go to SendMessageToDevice
        end
        return
    end
!    log URL cat ` - ` cat Response
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Get information about a device
GetDeviceData:
    put property `name` of Device into Name
    put property `ssid` of Device into SSID
    put from 8 of SSID into MAC
    if Device has property `path` put property `path` of Device into Path
    else
    begin
        put empty into Path
        set property `path` of Device to Path
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Post the config data
PostConfigData:
    if CurrentSSID is not HostSSID gosub to ConnectToHost
    save prettify Config to ConfigFile
!    log `Config: ` cat prettify Config
    if SystemMAC is empty return
    if SystemConfig is empty return
    put `https://rbrheating.com/ui/resources/php/rest.php/config/` cat SystemMAC cat `/` cat SystemPassword into URL
!    log URL cat ` ` cat prettify SystemConfig
!    log `Posting SystemConfig to ` cat URL
    post stringify SystemConfig to URL
    or begin
        put `I couldn't post the config file` into StatusMessage
        go to Error
    end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   The usual chain of JSON updates
UpdateSystemConfig:
    set property `devices` of SystemConfig to Devices
UpdateSystems:
    set property SystemName of Systems to SystemConfig
UpdateConfig:
    set property `systems` of Config to Systems
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Pause the system controller
PauseSystemController:
    if SystemIPAddr is not empty
        get Response from url `http://` cat SystemIPAddr cat `:17348/cgi-bin/pause.py`
        or begin
            put `Unable to contact server at ` cat IPAddr into StatusMessage
            goto Working
        end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Resume the system controller
ResumeSystemController:
    if SystemIPAddr is not empty
        get Response from url `http://` cat SystemIPAddr cat `:17348/cgi-bin/resume.py`
        or begin
            put `Unable to contact server at ` cat IPAddr into StatusMessage
            goto Working
        end
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   The Status box at the bottom of the screen
OK:
    put `OK` into StatusMessage

Idle:
    log `*** ` cat StatusMessage cat ` ***`
    set the text of StatusLabel to `<font color="#008000">` cat StatusMessage cat `</font>`
    clear ErrorFlag
    go to ManageButtonStates

Working:
    log `*** ` cat StatusMessage cat ` ***`
    set the text of StatusLabel to `<font color="#0000e0">` cat StatusMessage cat `</font>`
    clear ErrorFlag
    wait 50 ticks
    return

Abort:
    put `Abort` into StatusMessage

Error:
    log `*** ` cat StatusMessage cat ` ***`
    set the text of StatusLabel to `<font color="#ff0000">` cat StatusMessage cat `</font>`
    set ErrorFlag
    go to ManageButtonStates

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Enable or disable the various buttons
ManageButtonStates:
    if SystemHostSSID is not empty
        if SystemHostSSID is not HostSSID go to SetBusy
! ScanDevicesButton
    if the count of SystemsCombo is 0 disable ScanDevicesButton else enable ScanDevicesButton
! ClearSystemButton
    if the count of SystemsCombo is 0 disable ClearSystemButton else enable ClearSystemButton
! RemoveSlaveButton
    if the count of SlaveList is 0 disable RemoveSlaveButton else enable RemoveSlaveButton
! ScanSystemsButton
    enable ScanSystemsButton
! RemoveSystemButton
    if SystemsCombo is empty disable RemoveSystemButton else enable RemoveSystemButton
! MasterDeviceButton
    if MasterDeviceButton is empty disable MasterDeviceButton else enable MasterDeviceButton
! RelayOffButton
    enable RelayOffButton
! RelayOnButton
    enable RelayOnButton
! NameInput
! LEDInput
! RelayInput
! DHT22Input
! PathInput
    if DeviceSelected
    begin
        enable DeviceNameInput
        enable PathInput
        enable LEDPinInput
        enable RelayPinInput
        enable DHT22PinInput
        if not IsMaster enable PathInput
    end
    else
    begin
        disable DeviceNameInput
        disable PathInput
        disable LEDPinInput
        disable RelayPinInput
        disable DHT22PinInput
    end
! UpdateFilesButton
    if DeviceNameInput is empty disable UpdateFilesButton else enable UpdateFilesButton
! DeleteFileButton
    if DeviceNameInput is empty disable DeleteFileButton else enable DeleteFileButton
! UpdateWidgetDataButton
    if DeviceNameInput is empty disable UpdateWidgetDataButton else enable UpdateWidgetDataButton
    wait 10 ticks
    return

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!   Set the system to be busy (no buttons enabled)
SetBusy:
    disable ScanDevicesButton
    disable ClearSystemButton
    disable RemoveSlaveButton
    disable UpdateFilesButton
    disable DeleteFileButton
    disable ScanSystemsButton
    disable RemoveSystemButton
    disable MasterDeviceButton
    disable RelayOffButton
    disable RelayOnButton
    disable UpdateWidgetDataButton
    disable DeviceNameInput
    disable PathInput
    disable LEDPinInput
    disable RelayPinInput
    disable DHT22PinInput
    wait 10 ticks
    return
